//===== rAthena Script =======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6c
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//= 1.6b Added 'disable_items' command.
//= 1.6c Replaced function 'A_An' with "F_InsertArticle".
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//  ADD YOUR NPC HERE
//============================================================
prontera,163,174,6	script	Poring Quest Shop#1	998,{ callfunc "qshop",1,2; }
prontera,141,180,6	script	PVP Quest Shop#1	998,{ callfunc "qshop",3,4,5; }
prontera,163,170,6	script	Costume Quest Shop#1	998,{ callfunc "qshop",6; }
// prontera,163,175,6	script	Quest Shop#2	998,{ callfunc "qshop",1,2; }	// call the shop 1 and 2 defined below
// etc.. Add your Shop NPCs 'Quest Shop#XXX' here
//============================================================


// Script Core - DO NOT DUPLICATE THIS NPC !!!!!!!!!!!!!
//============================================================
-	script	quest_shop	-1,{
function Add; function Chk; function Slot;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .Announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],
		"#CASHPOINTS", "Cash Points";


//=====================================================================================
// ------------------- ADD YOUR SHOPS NAME AND ITEMS SHOPS STARTING HERE --------------
//=====================================================================================

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],
		"Headgears",		// Shop Named 1
		"Animal Headgears", // Shop Named 2
		"PVP Accessory",	// Shop Named 3
		"PVP Armor",	    // Shop Named 4
		"PVP Weapon",	    // Shop Named 5
		"Wings";	    	// Shop Named 6

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

// Shop 1 Headgear
	Add(1,5022,1,0,0,7086,1,969,10,999,40,1003,50,984,2);
	Add(1,5032,1,0,0,1059,250,2221,1,2227,1,7063,600);
	Add(1,5027,1,0,0,2252,1,1036,400,7001,50,4052,1);
	Add(1,5045,1,0,0,2252,1,1054,450,943,1200);

// Shop 2 Animal Headgear
	Add(2,5531,1,0,0,6480,100);
	Add(2,18629,1,0,0,6480,100);
	Add(2,5766,1,0,0,6480,100);
	Add(2,5611,1,0,0,6480,100);
	Add(2,5447,1,0,0,6480,100);
	Add(2,5572,1,0,0,6480,100);
	Add(2,5660,1,0,0,6480,100);
	Add(2,5431,1,0,0,6480,100);
	Add(2,5283,1,0,0,6480,100);
	Add(2,5554,1,0,0,6480,100);
	Add(2,5822,1,0,0,6480,100);

// Shop 3 PVP Accessory
	Add(3,2733,1,0,0,7642,30);
	Add(3,2720,1,0,0,7642,30);
	Add(3,2721,1,0,0,7642,30);
	Add(3,2722,1,0,0,7642,30);
	Add(3,2723,1,0,0,7642,30);
	Add(3,2724,1,0,0,7642,30);
	Add(3,2725,1,0,0,7642,30);

// Shop 4 PVP Armor
	Add(4,2538,1,0,0,7642,30);
	Add(4,2539,1,0,0,7642,30);
	Add(4,2540,1,0,0,7642,30);
	Add(4,2435,1,0,0,7642,30);
	Add(4,2436,1,0,0,7642,30);
	Add(4,2437,1,0,0,7642,30);
	Add(4,2376,1,0,0,7642,50);
	Add(4,2377,1,0,0,7642,50);
	Add(4,2378,1,0,0,7642,50);
	Add(4,2379,1,0,0,7642,50);
	Add(4,2380,1,0,0,7642,50);
	Add(4,2381,1,0,0,7642,50);
	Add(4,2382,1,0,0,7642,50);

// Shop 5 PVP Weapon
	Add(5,13036,1,0,0,7642,50);
	Add(5,13411,1,0,0,7642,50);
	Add(5,1183,1,0,0,7642,50);
	Add(5,1425,1,0,0,7642,50);
	Add(5,1632,1,0,0,7642,50);
	Add(5,1634,1,0,0,7642,50);
	Add(5,1543,1,0,0,7642,50);
	Add(5,1380,1,0,0,7642,50);
	Add(5,13305,1,0,0,7642,50);
	Add(5,1739,1,0,0,7642,50);
	Add(5,1279,1,0,0,7642,50);
	Add(5,1924,1,0,0,7642,50);
	Add(5,1978,1,0,0,7642,50);
	Add(5,1574,1,0,0,7642,50);
	Add(5,1824,1,0,0,7642,50);
	Add(5,13108,1,0,0,7642,50);
	Add(5,13172,1,0,0,7642,50);
	Add(5,13174,1,0,0,7642,50);
	Add(5,1482,1,0,0,7642,50);
	Add(5,13173,1,0,0,7642,50);
	Add(5,13171,1,0,0,7642,50);
	Add(5,1542,1,0,0,7642,50);
	Add(5,1635,1,0,0,7642,50);
	Add(5,1633,1,0,0,7642,50);	
	Add(5,1184,1,0,0,7642,50);
	Add(5,13410,1,0,0,7642,50);
	Add(5,13037,1,0,0,7642,50);
	Add(5,1823,1,0,0,7642,50);
	Add(5,1575,1,0,0,7642,50);
	Add(5,1977,1,0,0,7642,50);
	Add(5,1923,1,0,0,7642,50);
	Add(5,1280,1,0,0,7642,50);
	Add(5,1738,1,0,0,7642,50);
	Add(5,13306,1,0,0,7642,50);
	Add(5,1379,1,0,0,7642,50);

// Shop 6 Costume - Wings
	Add(6,20500,1,0,0,949,100,916,100,7115,100,7063,100);
	Add(6,20511,1,0,0,949,100,916,100,7115,100,7063,100);
	Add(6,20727,1,0,0,949,100,916,100,7115,100,7063,100);
	Add(6,20764,1,0,0,949,100,916,100,7115,100,7063,100);
	Add(6,20762,1,0,0,949,100,916,100,7115,100,7063,100);	
	Add(6,20761,1,0,0,949,100,916,100,7115,100,7063,100);
	Add(6,20737,1,0,0,949,100,916,100,7115,100,7063,100);
	Add(6,20502,1,0,0,949,100,916,100,7115,100,7063,100);
	Add(6,20746,1,0,0,949,100,916,100,7115,100,7063,100);
	Add(6,20510,1,0,0,949,100,916,100,7115,100,7063,100);
	Add(6,20509,1,0,0,949,100,916,100,7115,100,7063,100);
	Add(6,20516,1,0,0,949,100,916,100,7115,100,7063,100);

// -----------------------------------------------------------

//=====================================================================================
// ------------------- YOUR SHOPS AND ITEMS SHOPS HAVE BEEN ADDED ---------------------
//=====================================================================================

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set @shop_index, select(.menu$);
	else if (.@size == 1) set @shop_index, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set @shop_index, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[@shop_index] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "qshop"+@shop_index,1;
	npcshopattach "qshop"+@shop_index;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+@shop_index+"_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+@shop_index+"_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	disable_items;
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & EQP_HEAD_LOW) || (@qe[1] & EQP_HEAD_TOP) || (@qe[1] & EQP_HEAD_MID) || (@qe[1] & EQP_COSTUME_HEAD_TOP) || (@qe[1] & EQP_COSTUME_HEAD_MID) || (@qe[1] & EQP_COSTUME_HEAD_LOW) || (@qe[1] & EQP_GARMENT) || (@qe[1] & EQP_COSTUME_GARMENT)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) set Zeny, Zeny-(.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.Announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):callfunc("F_InsertArticle",getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(0)+"_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}
}

function	script	qshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop1	-1,909:-1
-	shop	qshop2	-1,909:-1
-	shop	qshop3	-1,909:-1
-	shop	qshop4	-1,909:-1
-	shop	qshop5	-1,909:-1
-	shop	qshop6	-1,909:-1
